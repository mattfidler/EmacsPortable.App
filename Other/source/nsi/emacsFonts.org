#+TITLE: Install Fonts for Emacs
#+AUTHOR: Matthew L. Fidler
#+PROPERTY: tangle EmacsFonts.nsi
* Introduction
If you like a particular font for editing with, it would be nice to
have it on all platforms.  This allows you to have the font on all
platforms if desired.
* Launcher Properties
** Setup Options
I want:
 - user level execution
 - CRC check
 - Good compression
#+BEGIN_SRC nsis
  CRCCheck On
  RequestExecutionLevel user
  ; Best Compression
  SetCompress Auto
  SetCompressor /SOLID lzma
  SetCompressorDictSize 32
  SetDatablockOptimize On
  Caption "Loading EmacsPortable.App"
  Subcaption 3 " "
  XPStyle on
  OutFile "..\..\..\App\eps\ep-font.exe"
  Icon "..\img\ico\appicon.ico"
  UninstallIcon "..\img\ico\trash_empty.ico"
  !include "LogicLib.nsh"
  !include "WinMessages.nsh"
  var EPEXE
#+END_SRC

** User Interface
EmacsPortable.App uses the loading bar to startup emacs.
#+BEGIN_SRC nsis 
  AutoCloseWindow true
  ChangeUI all "${NSISDIR}\Contrib\UIs\LoadingBar_Icon.exe"
#+END_SRC
* Setup Fonts
** Find Fonts
#+BEGIN_SRC nsis
  !macro AddFonts EXT
    DetailPrint "Adding ${EXT} fonts"
    FindFirst $0 $1 "$EPEXE\App\fonts\*${EXT}"
    ${While} $1 != ""
      GetFullPathName $2 "$EPEXE\App\fonts\$1"
      DetailPrint "Attempting to add Font Resource $2"
      System::Call 'Gdi32::AddFontResource(t "$2")i .r3'
      ${If} $3 == "0"
        DetailPrint "Failed"
      ${Else}
        DetailPrint "Sucessfully added $2 font(s)"
      ${EndIf}
      FindNext $0 $1
    ${EndWhile}
    FindClose $0
  !macroend
  !define AddFonts '!insertmacro AddFonts'
  
  !macro RmFonts EXT
    DetailPrint "Removing ${EXT} fonts"
    FindFirst $0 $1 "$EPEXE\App\fonts\*${EXT}"
    ${While} $1 != ""
      GetFullPathName $2 "$EPEXE\App\fonts\$1"
      DetailPrint "Removing Font Resource $2"
      System::Call 'Gdi32::RemoveFontResource(t "$2")i .r3'
      ${If} $3 == "0"
        DetailPrint "Failed"
      ${Else}
        DetailPrint "Successfully Removed"
      ${EndIf}
      FindNext $0 $1
    ${EndWhile}
    FindClose $0
  !macroend
  !define RmFonts '!insertmacro RmFonts'
  
#+END_SRC

* Main
#+BEGIN_SRC nsis
  Section "Main" sec_main
    HideWindow
    IfFileExists "$TEMP\rm-ep-font.exe" end
    IfFileExists "$TEMP\ep\rm-ep-font.exe" end
    IfFileExists "..\..\EmacsPortableApp.exe" 0 +3
    GetFullPathName /SHORT $EPEXE "..\.."
    Goto +2
    ReadINIStr $EPEXE "$EXEDIR\ep.ini" "EmacsPortableApp" "EXEDIR"
    ReadIniStr $R0 "$EPEXE\Data\ini\EmacsPortableApp.ini" "EmacsPortableApp" "LiberKey"
    ClearErrors
    StrCmp $R0 "1" 0 end
    DetailPrint "Add Fonts"
    ${AddFonts} .fon
    ${AddFonts} .fnt
    ${AddFonts} .ttf
    ${AddFonts} .ttc
    ${AddFonts} .fot
    ${AddFonts} .otf
    ${AddFonts} .mmm
    ${AddFonts} .pfb
    ${AddFonts} .pfm
    SendMessage ${HWND_BROADCAST} ${WM_FONTCHANGE} 0 0 /TIMEOUT=5000
    writeUninstaller "$TEMP\ep\rm-ep-font.exe"
    end:
      ClearErrors
  SectionEnd
#+END_SRC
* Uninstall
#+BEGIN_SRC nsis
  Section "Uninstall" sec_uninstall 
    SetAutoClose true
    Delete "$TEMP\ep\rm-ep-font.exe"
    ${If} ${FileExists} "..\..\EmacsPortableApp.exe"
      GetFullPathName /SHORT $EPEXE "..\.."
    ${ElseIf} ${FileExists} "$TEMP\ep.ini"
      ReadINIStr $EPEXE "$TEMP\ep.ini" "EmacsPortableApp" "EXEDIR"
    ${ElseIf} ${FileExists} "$TEMP\ep\ep.ini"
      ReadINIStr $EPEXE "$TEMP\ep\ep.ini" "EmacsPortableApp" "EXEDIR"
    ${ElseIf} ${FileExists} "$EXEDIR\ep.ini"
      ReadINIStr $EPEXE "$EXEDIR\ep.ini" "EmacsPortableApp" "EXEDIR"
    ${EndIf}
    ClearErrors
    ${If} $EPEXE != ""
      DetailPrint "EpEXE: $EPEXE"
      ${RmFonts} .fon
      ${RmFonts} .fnt
      ${RmFonts} .ttf
      ${RmFonts} .ttc
      ${RmFonts} .fot
      ${RmFonts} .otf
      ${RmFonts} .mmm
      ${RmFonts} .pfb
      ${RmFonts} .pfm
      SendMessage ${HWND_BROADCAST} ${WM_FONTCHANGE} 0 0 /TIMEOUT=5000
    ${EndIf}
  SectionEnd
#+END_SRC nsis
