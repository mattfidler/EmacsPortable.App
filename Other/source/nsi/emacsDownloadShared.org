#+TITLE: Shared Download Code
#+AUTHOR: Matthew L. Fidler
#+PROPERTY: tangle EmacsDownloadShared.nsh
* Macros
** Download Emacs Binaries
#+BEGIN_SRC nsis
  !define getEmacsWin `!insertmacro getEmacsWin`
  !macro getEmacsWin VER
    ReadINIStr $R0 "${mirror}" "${VER}" "win"
    inetc::get  $R0 "$TEMP\emacs.zip"
    ZipDLL::extractall "$TEMP\emacs.zip" "$INSTDIR\App\"
    Delete "$TEMP\emacs.zip"
  !macroend
  !define getEmacsMac `!insertmacro getEmacsMac`
  !macro getEmacsMac VER
    IfFileExists "$INSTDIR\App\emacs-${VER}" +3
    DetailPrint "Cannot Locate Windows Emacs Binaries for Emacs ${VER}, Skipping Mac install."
    Goto end
    ReadINIStr $R0 "${mirror}" "${VER}" "mac"
    inetc::get $R0 "$TEMP\emacsMac.zip"
    ZipDLL::extractall "$TEMP\emacsMac.zip" "$INSTDIR\App\emacs-${VER}"
    Delete "$TEMP\emacsMac.zip"
    end:
  !macroend
  
#+END_SRC


** Mark Installed Macros
This marks a section as installed and makes it read-only
#+BEGIN_SRC nsis
  !define ifSecNotRO `!insertmacro ifSecNotRO`
  !macro ifSecNotRO SEC JMP_IF_TRUE
    ClearErrors
    SectionGetFlags ${SEC} $0
    IfErrors 0 +4
    ClearErrors
    MessageBox MB_OK "Section ${SEC} out of range"
    Goto ${JMP_IF_TRUE}
    IntOp $0 $0 & ${SF_RO}
    StrCmp "0" "$0" ${JMP_IF_TRUE}
  !macroend
  
  
  !define ifSecRO `!insertmacro ifSecRO`
  !macro ifSecRO SEC JMP_IF_TRUE
    ClearErrors
    SectionGetFlags ${SEC} $0
    IfErrors 0 +4
    ClearErrors
    MessageBox MB_OK "Section ${SEC} out of range"
    Goto +3
    IntOp $0 $0 & ${SF_RO}
    StrCmp "0" "$0" 0 ${JMP_IF_TRUE}
  !macroend
  
  !define ifSecNotHidden `!insertmacro ifSecNotHidden`
  !macro ifSecNotHidden SEC JMP
    ClearErrors
    SectionGetText ${SEC} $0
    IfErrors 0 +4
    ClearErrors
    MessageBox MB_OK "Section ${SEC} out of range"
    Goto +2
    StrCmp "" "$0" 0 ${JMP}
  !macroend
  
  !define setInstallIfExists `!insertmacro setInstallIfExists`
  !macro setInstallIfExists FILE SEC
    ClearErrors
    IfFileExists "${FILE}" 0 +10
    SectionGetFlags ${SEC} $0
    IfErrors 0 +3
    MessageBox MB_OK "Section ${SEC} out of range$\nIfFileExists: ${FILE}"
    Goto +6
    IntOp $0 ${SF_RO} | $0
    SectionSetFlags ${SEC} $0
    SectionGetText ${SEC} $0
    StrCpy $0 "$0 (Installed)"
    SectionSetText ${SEC} $0
  !macroend
  
  !define setInstall `!insertmacro setInstall`
  !macro setInstall SEC
    SectionGetFlags ${SEC} $0
    IfErrors 0 +3
    MessageBox MB_OK "Section ${SEC} out of range"
    Goto +6
    IntOp $0 ${SF_RO} | $0
    SectionSetFlags ${SEC} $0
    SectionGetText ${SEC} $0
    StrCpy $0 "$0 (Installed)"
    SectionSetText ${SEC} $0
  !macroend
  
  !define setInstallGroup `!insertmacro setInstallGroup`
  !macro setInstallGroup SEC
    ClearErrors
    SectionGetFlags ${SEC} $0
    IfErrors 0 +3
    MessageBox MB_OK "Section ${SEC} out of range"
    Goto +6
    IntOp $0 ${SF_RO} | $0
    SectionSetFlags ${SEC} $0
    SectionGetText ${SEC} $0
    StrCpy $0 "$0 (Installed)"
    SectionSetText ${SEC} $0
  !macroend
  
  !define setChkRO `!insertmacro setChkRO`
  
  !macro setChkRO SEC
    ${ifSecRO} ${SEC} is_ro_dont_check_${SEC}
    SectionSetFlags ${SEC} 17
    is_ro_dont_check_${SEC}:
      ClearErrors
  !macroend
  
#+END_SRC
** Select Section If Other section is selected
When this a section SEC is selected, either check OTHER_SEC or leave
it alone when OTHER_SEC is read-only.
#+BEGIN_SRC nsis
  !define selectSectionIf `!insertmacro selectSectionIf`
  !macro selectSectionIf SEC_CHECKED SEC_TO_SELECT
    SectionGetFlags ${SEC_CHECKED} $0
    IntOp $0 $0 & ${SF_SELECTED}
    StrCmp "$0" "0" +5 0
    SectionGetFlags ${SEC_TO_SELECT} $0
    IntOp $0 $0 & ${SF_RO}
    StrCmp "$0" "0" 0 +2
    SectionSetFlags ${SEC_TO_SELECT} ${SF_SELECTED}
  !macroend
  
#+END_SRC


** Portable App Macro
#+BEGIN_SRC nsis
  !macro installPortableApp WHAT
    ReadINIStr $R0 "${mirror}" "${WHAT}" "url"
    IfErrors end
    ReadINIStr $R9 "${mirror}" "${WHAT}" "type"
    IfErrors exe_type
    StrCpy $R9 ".$R9"
    Goto use_inetc
    exe_type:
      ClearErrors
      StrCpy $R9 ".exe"
    use_inetc:
      inetc::get /CONNECTTIMEOUT 20 /NOCOOKIES /TRANSLATE "Downloading ..." \
          "Connecting..." second minute hour s "%dkB (%d%%) of %dkB @ %d.%01dkB/s" \
          " (%d %s%s remaining)" $R0 "$TEMP\portable$R9" /END
      IfFileExists "$TEMP\portable$R9" found not_found
    not_found:
      IfFileExists "$INSTDIR\App\gw32\wget.exe" +3 0
      DetailPrint "Wget not found in App\gw32"
      Goto end
      DetailPrint "Download of ${WHAT} failed, trying wget with proxy setting set"
      ${SetupProxy}
      IfFileExists "$INSTDIR\Data\ini\proxy-$PROXY_IDE.ini" read_proxy set_proxy
    set_proxy:
      IfFileExists "$INSTDIR\App\eps\set-proxy.exe" +3 0
      DetailPrint "Could not set proxy, assume no proxy settings are needed"
      Goto wget
      ExecWait "$INSTDIR\App\eps\set-proxy.exe"
      goto read_proxy
    read_proxy:
      IfFileExists "$INSTDIR\Data\ini\proxy-$PROXY_IDE.ini" 0 wget
      ReadIniStr $R0 "$INSTDIR\Data\ini\proxy-$PROXY_IDE.ini" "Proxy" "Server"
      ${BlowFish_Decrypt} $R0 $R0 "$PROXY_ID"
      StrCmp "$R0" "" end
      StrCpy "$R1" "$R0"
      ReadIniStr "$R0" "$INSTDIR\Data\ini\proxy-$PROXY_IDE.ini" "Proxy" "Port"
      ${BlowFish_Decrypt} $R0 $R0 "$PROXY_ID"
      StrCmp "$R0" "" +2 0
      StrCpy "$R1" "$R1:$R0"
      ReadIniStr "$R0" "$INSTDIR\Data\ini\proxy-$PROXY_IDE.ini" "Proxy" "User"
      ${BlowFish_Decrypt} $R0 $R0 "$PROXY_ID"
      StrCmp "$R0" "" +2 0
      StrCpy "$R2" "$R0"
      ReadIniStr "$R0" "$INSTDIR\Data\ini\proxy-$PROXY_IDE.ini" "Proxy" "Password"
      ${BlowFish_Decrypt} $R0 $R0 "$PROXY_ID"
      StrCmp "$R0" "" +3 0
      StrCmp "$R2" "" +2 0
      StrCpy "$R2" "$R2:$R0"
      StrCmp "$R2" "" +2 0
      StrCpy "$R1" "$R2@$R1"
      System::Call 'Kernel32::SetEnvironmentVariableA(t, t) i("HTTP_PROXY","http://$R1").r0'
      System::Call 'Kernel32::SetEnvironmentVariableA(t, t) i("HTTPS_PROXY","http://$R1").r0'
      System::Call 'Kernel32::SetEnvironmentVariableA(t, t) i("FTP_PROXY","ftp://$R1").r0'
      ${PathIfExist} "$INSTDIR\App\gw32"
    wget:
      ReadINIStr $R0 "${mirror}" "${WHAT}" "url"
      ExecWait "$INSTDIR\App\gw32\wget.exe $R0 -O$TEMP\portable$R9"
      IfFileExists "$TEMP\portable$R9" found 0
      DetailPrint "Could not get file with wget either, skipping."
    found:
      StrCmp "$R9" ".exe" is_exe
      StrCmp "$R9" ".7z" is_7z
      Goto end
    is_7z:
      ReadINIStr $R0 "${mirror}" "${WHAT}" "out"
      ClearErrors
      MessageBox MB_OK "$PA\$R0"
      SetOutPath "$PA\$R0"
      Nsis7z::ExtractWithDetails "$TEMP\portable.7z" "Installing package ${WHAT}..."
      Delete "$TEMP\portable.7z"
      Goto end
    is_exe:
      ExecWait "$TEMP\portable.exe"
      Delete "$TEMP\portable.exe"
    end:
      ClearErrors
  !macroend
  !define installPA `!insertmacro installPortableApp`
  
#+END_SRC
** Emacs Version Initialization 
#+BEGIN_SRC nsis
  !macro INI_VERS
    StrCpy $nemacs 0
    ${Locate} "$INSTDIR\App" "/L=D /M=emacs-* /S= /G=0" "SetValues"
    StrCmp "$INSTDIR" "$EXEDIR" +2
    StrCpy $nemacs 0
    StrCmp "$nemacs" "0" 0 +3
    SectionSetFlags ${sec_emacs_default} ${SF_SELECTED}
    SectionSetFlags ${sec_emacs_default_mac} ${SF_SELECTED}
  !macroend
  !define INI_VERS `!insertmacro INI_VERS`
  
#+END_SRC

** Get Full Plugin Macro
#+BEGIN_SRC nsis
!macro getPluginFull NAME
    StrCmp "$PA" "" 0 download
    ${GetDrives} "FDD+HDD" "GetDriveVars"
    StrCmp "$PA" "" 0 download
    MessageBox MB_OK "Cannot Install ${NAME} Plugin; Can't determine PortableApps Location."
    download:
      ReadINIStr $R0 "${mirror}" "nsis:${NAME}" "url"
      inetc::get $R0 "$TEMP\${NAME}.zip"
      ZipDLL::extractall "$TEMP\${NAME}.zip" "$PA\NSISPortableANSI\App\NSIS"
      Delete "$TEMP\${NAME}.zip"
    end:
      #
  !macroend
#+END_SRC


** Path if Exist Macro
#+BEGIN_SRC nsis
!macro _PathIfExist ARG1
  DetailPrint "Checking for ${ARG1}"
  StrCpy $9 ""
  IfFileExists "${ARG1}" 0 +4
  System::Call 'Kernel32::GetEnvironmentVariable(t , t, i) i("PATH", .r0, ${NSIS_MAX_STRLEN}).r1'
  System::Call 'Kernel32::SetEnvironmentVariableA(t, t) i("PATH", "${ARG1};$0").r3'StrCpy $9 "1"
!macroend

!define PathIfExist '!insertmacro "_PathIfExist"'
#+END_SRC
** Download Zip File Macro
#+BEGIN_SRC nsis
  !define DOWN '!insertmacro DOWN'  
  !macro DOWN VAL OUT
    SectionGetFlags ${sec_use_git_instead_of_zip} $0
    IntOp $0 $0 & ${SF_SELECTED}
    StrCmp "0" $0 zip git
    zip:
      ReadIniStr $R1 "${mirror}" "${VAL}" "ver"
      ReadIniStr $R0 "${mirror}" "${VAL}" "base"
      ReadIniStr $R2 "${mirror}" "${VAL}" "ext"
      inetc::get "$R0$R1$R2" "$TEMP\${VAL}.zip"
      IfFileExists "$TEMP\${VAL}.zip" +3 0
      DetailPrint "Could not Download ${VAL}, skipping."
      Goto +2
      ZipDLL::extractall "$TEMP\${VAL}.zip" "${OUT}"
      Delete "$TEMP\${VAL}.zip"
      Goto end
    git:
      ReadIniStr $R0 "${mirror}" "${VAL}" "git"
      ReadIniStr $R1 "${mirror}" "${VAL}" "gitout"
      IfErrors 0 +3
      ClearErrors
      Goto zip
      ClearErrors
      DetailPrint "$PG clone $R0 ${OUT}\$R1"
      ExecWait "$PG clone $R0 ${OUT}\$R1"
      ReadIniStr "$R0" "${mirror}" "${VAL}" "github"
      IfErrors end
      ExecWait "$PG remote add --track master github $R0"
      Goto end
    end:
      ClearErrors
  !macroend
  
#+END_SRC


** GnuWin32
*** GnuWin32 Utilities Download
#+BEGIN_SRC nsis
  !macro  g32exist WHAT GO
    IfFileExists "$INSTDIR\App\ini\gw32.ini" 0 g32exist_skip
    
    g32exist_skip:
      ClearErrors
  !macroend
  
  !macro g32down WHAT
    ReadINIStr $R0 "${mirror}" "gw32:${WHAT}" "bin.exe"
    IfErrors g32_zip
    SetOutPath "$INSTDIR\App\gw32\bin"
    inetc::get /CONNECTTIMEOUT 30 $R0 "$INSTDIR\App\gw32\bin\${WHAT}.exe"
    WriteIniStr "$INSTDIR\App\ini\gw32.ini" "${WHAT}.bin" "${WHAT}.exe" "1"
    Goto g32_exit
    g32_zip:
      ReadINIStr $R0 "${mirror}" "gw32:${WHAT}" "bin.zip"
      IfErrors g32_download
      inetc::get /CONNECTTIMEOUT 30 $R0 "$TEMP\${WHAT}-bin.zip" 
      IfFileExists "$TEMP\${WHAT}-bin.zip" 0 g32_exit
      ZipDLL::extractall "$TEMP\${WHAT}-bin.zip" "$TEMP\ep-gw32"
      StrCpy $R0 "${WHAT}"
      StrCpy $R1 "bin"
      ${Locate} "$TEMP\ep-gw32" "" "AddGW"
      RmDir /r "$TEMP\ep-gw32"
      Delete "$TEMP\${WHAT}-bin.zip" 
      ReadINIStr $R0 "${mirror}" "gw32:${WHAT}" "dep.zip"
      IfErrors g32_exit
      inetc::get /CONNECTTIMEOUT 30 $R0 "$TEMP\${WHAT}-dep.zip"
      IfFileExists "$TEMP\${WHAT}-dep.zip" 0 g32_exit
      RmDir /r "$TEMP\ep-gw32"
      ZipDLL::extractall "$TEMP\${WHAT}-dep.zip" "$TEMP\ep-gw32"
      SetOutPath "$INSTDIR\App\gw32"
      StrCpy $R0 "${WHAT}"
      StrCpy $R1 "dep"
      ${Locate} "$TEMP\ep-gw32" "" "AddGW"
      RmDir /r "$TEMP\ep-gw32"
      Delete "$TEMP\${WHAT}-dep.zip"
    g32_download:
      ClearErrors
      inetc::get /CONNECTTIMEOUT 30 \
          "http://gnuwin32.sourceforge.net/downlinks/${WHAT}-bin-zip.php" \
          "$TEMP\${WHAT}-bin.zip" 
      IfFileExists "$TEMP\${WHAT}-bin.zip" 0 g32_exit
      RmDir /r "$TEMP\ep-gw32"
      ZipDLL::extractall "$TEMP\${WHAT}-bin.zip" "$TEMP\ep-gw32"
      SetOutPath "$INSTDIR\App\gw32"
      StrCpy $R0 "${WHAT}"
      StrCpy $R1 "bin"
      ${Locate} "$TEMP\ep-gw32" "" "AddGW" 
      RmDir /r "$TEMP\ep-gw32"
      Delete "$TEMP\${WHAT}-bin.zip"
      inetc::get /CONNECTTIMEOUT 30 \
          "http://gnuwin32.sourceforge.net/downlinks/${WHAT}-dep-zip.php" \
          "$TEMP\${WHAT}-dep.zip"
      IfFileExists "$TEMP\${WHAT}-dep.zip" 0 g32_exit
      RmDir /r "$TEMP\ep-gw32"
      ZipDLL::extractall "$TEMP\${WHAT}-dep.zip" "$TEMP\ep-gw32"
      SetOutPath "$INSTDIR\App\gw32"
      StrCpy $R1 "dep"
      ${Locate} "$TEMP\ep-gw32" "" "AddGW"
      RmDir /r "$TEMP\ep-gw32"
      Delete "$TEMP\${WHAT}-dep.zip"
    g32_exit:
      ClearErrors
  !macroend
  
  Function AddGW
    StrLen $0 "$TEMP\ep-gw32\"
    StrCpy "$R4" "$R8" "" $0
    StrCpy "$R5" "$R9" "" $0
    StrCmp $R6 "" is_dir is_file
    is_dir:
      CreateDirectory "$INSTDIR\App\gw32\$R5"
      Goto end
    is_file:
      WriteINIStr "$INSTDIR\App\ini\gw32.ini" "$R0.$R1" "$R5" "1"
      StrCmp "$R1" "dep" 0 skip_dep
      WriteINIStr "$INSTDIR\App\ini\gw32.ini" "$R5" "$R0" "1"
    skip_dep:
      CopyFiles /SILENT "$TEMP\ep-gw32\$R5" "$INSTDIR\App\gw32\$R4"
    end:
      ClearErrors
      StrCpy $0 1
      Push $0
  FunctionEnd
  
#+END_SRC

*** GnuWin32 Remove 
This removes a gnuwin32 package.
#+BEGIN_SRC nsis
  !macro g32rm INI PATH WHAT
    IfFileExists "${INI}" 0 g32rmnotinstalled_${WHAT}
    EnumINI::SectionExist "${INI}" "${WHAT}.bin"
    Pop $R0
    StrCmp "$R0" "1" 0 g32rmnotinstalled_${WHAT}
    EnumINI::Section "${INI}" "${WHAT}.bin"
    Pop $R0
    StrCmp "$R0" error g32rmnotinstalled_${WHAT}
    loop_g32rm_${WHAT}:
      IntCmp $R0 "0" loop_g32rm_${WHAT}_done loop_g32rm_${WHAT}_done 0
      Pop $R1
      Delete "${PATH}\$R1"
      IntOp $R0 $R0 - 1
      Goto loop_g32rm_${WHAT}
    loop_g32rm_${WHAT}_done:
      DeleteIniSec "${INI}" "${WHAT}.bin"
      ## Try to remove dependencies
      EnumINI::Section "${INI}" "${WHAT}.dep"
      Pop $R0
      StrCmp "$R0" error g32rmnotinstalled_${WHAT}
    loop_g32rm_dep_${WHAT}:
      IntCmp $R0 "0" loop_g32rm_dep_${WHAT}_done loop_g32rm_dep_${WHAT}_done 0
      Pop $R1
      EnumINI::Section "${INI}" "$R1"
      Pop $R2
      StrCmp "$R2" error g32nodep_${WHAT}
      StrCmp "$R2" "1" g32depdel_${WHAT}
    loop_g32rm_dep_${WHAT}2:
      IntCmp $R2 "0" loop_g32rm_dep_${WHAT}_done2 loop_g32rm_dep_${WHAT}_done2 0
      Pop $R3
      IntOp $R2 $R2 - 1
      Goto loop_g32rm_dep_${WHAT}2
    loop_g32rm_dep_${WHAT}_done2:
      DeleteIniStr "${INI}" "$R1" "${WHAT}"
      Goto g32nodep_${WHAT}
    g32depdel_${WHAT}:
      Pop $R2
      StrCmp "$R2" "${WHAT}" 0 g32nodep_${WHAT}
      Delete "${PATH}\$R1"
      DeleteIniSec "${INI}" "${WHAT}.dep"
    g32nodep_${WHAT}:
      IntOp $R0 $R0 - 1
      Goto loop_g32rm_dep_${WHAT}
    loop_g32rm_dep_${WHAT}_done: 
      DeleteIniSec "${INI}" "${WHAT}.dep"
      
    g32rmnotinstalled_${WHAT}:
    ClearErrors
  !macroend
  !define g32rm `!insertmacro g32rm "$INSTDIR\App\ini\gw32.ini" "$INSTDIR\App\gw32"`
#+END_SRC

*** GnuWin32 Is Installed?
#+BEGIN_SRC nsis
  !macro g32installed PREFIX INI PATH WHAT SEC
    IfFileExists "${INI}" 0 "${PREFIX}notinstalled_${WHAT}_${SEC}"
    EnumINI::SectionExist "${INI}" "${WHAT}.bin"
    Pop $R0
    StrCmp "$R0" "1" 0 "${PREFIX}notinstalled_${WHAT}_${SEC}"
    EnumINI::Section "${INI}" "${WHAT}.bin"
    Pop $R0
    StrCmp "$R0" "error" "${PREFIX}notinstalled_${WHAT}_${SEC}"
    StrCpy $R3 "1"
    "loop_${PREFIX}_${WHAT}_${SEC}:"
      IntCmp $R0 "0" "loop_${PREFIX}_${WHAT}_${SEC}_done" "loop_${PREFIX}_${WHAT}_${SEC}_done" 0
      Pop $R1
      StrCmp $R3 "0" +3
      IfFileExists "${PATH}\$R1" +2
      StrCpy $R3 "0"
      IntOp $R0 $R0 - 1
      Goto "loop_${PREFIX}_${WHAT}_${SEC}"
    "loop_${PREFIX}_${WHAT}_${SEC}_done:"
      StrCmp $R3 "0" "${PREFIX}notinstalled_${WHAT}_${SEC}" "${PREFIX}installed_${WHAT}_${SEC}"
    "${PREFIX}installed_${WHAT}_${SEC}:"
      SectionGetFlags ${SEC} $0
      IntOp $0 ${SF_RO} | $0
      SectionSetFlags ${SEC} $0
      SectionGetText ${SEC} $0
      StrCpy $0 "$0 (Installed)"
      SectionSetText ${SEC} $0
      "${PREFIX}notinstalled_${WHAT}_${SEC}:"
      ClearErrors
      
  !macroend
  !define g32installed `!insertmacro g32installed "g32" "$INSTDIR\App\ini\gw32.ini" "$INSTDIR\App\gw32"`
  !macro g32removed PREFIX INI PATH WHAT SEC
    IfFileExists "${INI}" 0 "r${PREFIX}notinstalled_${WHAT}_${SEC}"
    EnumINI::SectionExist "${INI}" "${WHAT}.bin"
    Pop $R0
    StrCmp "$R0" "1" 0 "r${PREFIX}notinstalled_${WHAT}_${SEC}"
    EnumINI::Section "${INI}" "${WHAT}.bin"
    Pop $R0
    StrCmp "$R0" "error" "r${PREFIX}notinstalled_${WHAT}_${SEC}"
    StrCpy $R3 "1"
    "loop_r${PREFIX}_${WHAT}_${SEC}:"
    IntCmp $R0 "0" "loop_r${PREFIX}_${WHAT}_${SEC}_done" "loop_r${PREFIX}_${WHAT}_${SEC}_done" 0
    Pop $R1
    StrCmp $R3 "0" +3
    IfFileExists "${PATH}\$R1" +2
    StrCpy $R3 "0"
    IntOp $R0 $R0 - 1
    Goto "loop_r${PREFIX}_${WHAT}_${SEC}"
    "loop_r${PREFIX}_${WHAT}_${SEC}_done:"
    StrCmp $R3 "0" "r${PREFIX}notinstalled_${WHAT}_${SEC}" "r${PREFIX}installed_${WHAT}_${SEC}"
    "r${PREFIX}notinstalled_${WHAT}_${SEC}:"
    SectionGetFlags ${SEC} $0
    IntOp $0 ${SF_RO} | $0
    SectionSetFlags ${SEC} $0
    SectionSetText ${SEC} ""
    "r${PREFIX}installed_${WHAT}_${SEC}:"
    ClearErrors
    
  !macroend
  !define g32removed `!insertmacro g32removed "g32" "$INSTDIR\App\ini\gw32.ini" "$INSTDIR\App\gw32"`
  
#+END_SRC

** EzWindows Ports
*** EzWindows Download 
#+BEGIN_SRC nsis
  !macro  ezwinexist WHAT GO              
    IfFileExists "$INSTDIR\App\ini\ezwin.ini" 0 ezwinexist_skip
    ezwinexist_skip:
      ClearErrors
  !macroend
  
  !macro ezwindown WHAT
    ClearErrors
    ReadIniStr $R0 "${mirror}" "ezw:${WHAT}" "ver"
    IfErrors ezwin_exit 
    inetc::get /CONNECTTIMEOUT 30 \
        "http://downloads.sourceforge.net/project/ezwinports/${WHAT}-$R0-bin.zip" \
        "$TEMP\${WHAT}-bin.zip" 
    IfFileExists "$TEMP\${WHAT}-bin.zip" 0 ezwin_exit
    RmDir /r "$TEMP\ep\ezwin"
    ZipDLL::extractall "$TEMP\${WHAT}-bin.zip" "$TEMP\ep-ezwin"
    SetOutPath "$INSTDIR\App\ezwin"
    StrCpy $R0 "${WHAT}"
    ${Locate} "$TEMP\ep-ezwin" "" "AddEZW"
    Sleep 5
    RmDir /r "$TEMP\ep-ezwin"
    Delete "$TEMP\${WHAT}-bin.zip"
    ezwin_exit:
      ClearErrors
  !macroend
  
  !macro InsertAddEZW
    Function AddEZW
      StrLen $R2 "$TEMP\ep-ezwin\"
      StrCpy "$R4" "$R8" "" $R2
      StrCpy "$R5" "$R9" "" $R2
      StrCmp $R6 "" is_dir is_file
      is_dir:
        CreateDirectory "$INSTDIR\App\ezwin\$R5"
        Goto end
      is_file:
        IfFileExists "$INSTDIR\App\ezwin\$R5" found_file new_file
      found_file:
        WriteINIStr "$INSTDIR\App\ini\ezwin.ini" "$R0.dep" "$R5" "1"
        WriteINIStr "$INSTDIR\App\ini\ezwin.ini" "$R5" "$R0" "1"
        ## Look for the same file in one of the binary sections
        Call PushEZW
        Pop $R1
      loop_look_bin:
        IntCmp $R1 0 end end 0
        Pop $R2
        StrCmp "$R2" "$R0" next_iter
        EnumINI::SectionExist "$INSTDIR\App\ini\ezwin.ini" "$R2.bin"
        Pop $R3
        IntCmp "$R3" "0" next_iter
        EnumINI::KeyExist "$INSTDIR\App\ini\ezwin.ini" "$R2.bin" "$R5"
        Pop $R3
        IntCmp "$R3" "0" next_iter
        DeleteIniStr "$INSTDIR\App\ini\ezwin.ini" "$R2.bin" "$R5"
        WriteINIStr "$INSTDIR\App\ini\ezwin.ini" "$R2.dep" "$R5" "1"
        WriteINIStr "$INSTDIR\App\ini\ezwin.ini" "$R5" "$R2" "1"
      next_iter:
        IntOp $R1 $R1 - 1
        Goto loop_look_bin
      new_file:
        WriteINIStr "$INSTDIR\App\ini\ezwin.ini" "$R0.bin" "$R5" "1" 
        CopyFiles /SILENT "$TEMP\ep-ezwin\$R5" "$INSTDIR\App\ezwin\$R4"
      end:
        ClearErrors
        Push $R5
    FunctionEnd
  !macroend
  
#+END_SRC
*** EzWindows Add/Remove/Exists
#+BEGIN_SRC nsis
  !define ezwinrm `!insertmacro g32rm  "ezwin" "$INSTDIR\App\ini\ezwin.ini" "$INSTDIR\App\ezwin"`
  !define ezwininstalled `!insertmacro g32installed "ezwin" "$INSTDIR\App\ini\ezwin.ini" "$INSTDIR\App\ezwin"`
  !define ezwinremoved `!insertmacro g32removed "ezwin" "$INSTDIR\App\ini\ezwin.ini" "$INSTDIR\App\ezwin"`
  
#+END_SRC

